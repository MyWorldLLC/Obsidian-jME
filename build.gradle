def getGitVersion(){
    "git describe --tags --always --first-parent".execute().text.trim()
}

def getGitCredentials(){
    def process = "git credential fill".execute()
    def stream = new PrintStream(process.getOutputStream(), true)
    stream.println("url=https://github.com/")
    stream.close()

    process.text.trim().split("\n").collectEntries {it.split("=")}
}

def getGitUser(){
    System.getenv("GITHUB_USER") ?: gitCredentials["username"]
}

def getGitPassword(){
    System.getenv("GITHUB_TOKEN") ?: gitCredentials["password"]
}

def getPlatform(){
    def platform = System.getProperty('os.name').toLowerCase()
    if(platform.contains('win')){
        platform = 'win'
    }else if(platform.contains('mac')){
        platform = 'mac'
    }else if(platform.contains('linux')){
        platform = 'linux'
    }
    return platform
}

def getArchitecture(){
    System.getProperty('os.arch').toLowerCase().startsWith('aarch64') ? 'amd64' : 'x64'
}

allprojects {
    ext {
        obsidianVersion = '1.0.9-13-g042d1a2'
        jmeVersion = '3.5.2-stable'
        jdkVersion = 17
        platform = this.platform
        architecture = this.architecture
        gitVersion = this.gitVersion
        gitCredentials = this.gitCredentials
        gitUser = this.gitUser
        gitPassword = this.gitPassword
    }
}